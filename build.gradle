plugins {
    id "fabric-loom" version "0.2.1-SNAPSHOT"
    id "maven-publish"
    id "signing"
}

group = "com.jamieswhiteshirt"
archivesBaseName = "developer-mode"
version = "1.0.11"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

minecraft {
}

dependencies {
    minecraft "com.mojang:minecraft:1.14 Pre-Release 5"
    mappings "net.fabricmc:yarn:1.14 Pre-Release 5+build.2"
    modCompile "net.fabricmc:fabric-loader:0.4.1+build.126"

    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

project.ext.isReleaseVersion = !version.endsWith("-SNAPSHOT")

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(jar.archivePath) {
                builtBy remapJar
            }
            artifact(sourcesJar.archivePath) {
                classifier = "sources"
                builtBy remapSourcesJar
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://maven.jamieswhiteshirt.com/libs-release/'
            def snapshotsRepoUrl = 'https://maven.jamieswhiteshirt.com/libs-snapshot/'
            url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl

            credentials {
                username project.properties.mavenUser
                password project.properties.mavenPassword
            }
        }
    }
}

signing {
    required = isReleaseVersion
    sign publishing.publications.mavenJava
}
